# shellcheck shell=sh disable=SC3006,3010,3043,2034,2154,3054,3020,2004,2164,3030,2015,2086,1036
emulate -RL zsh
setopt extendedglob warncreateglobal typesetsilent noshortloops

(( ${ICE[test]:-1} )) || return 0

[[ "$1" = plugin ]] && \
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="$5" || \
    local type="$1" url="$2" id_as="$3" dir="$4"

(( ${+ICE[notest]} )) && return 0

(
    builtin cd -q $dir

    if [[ -f Makefile ]]; then
        â€¦zp-test-make-targets Makefile
        if (( ${reply[(I)test]} )); then
            [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                print -P -- "%F{38}test z-plugin: %F{154}RUNNING MAKEFILE TESTS%f"
            integer quiet=0
            zstyle -T ":zi:annex:test" quiet && quiet=1
            if (( quiet )); then
                if make test &>/dev/null; then
                    [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                        print -P -- "%F{38}test z-plugin: %F{154}The tests succeeded%f"
                else
                    [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                        print -P -- "%F{38}test z-plugin: %F{160}The tests have failed%f"
                fi
            else
                make test
            fi
        fi
    fi
    local -a farray
    farray=( **/*.zunit(N) )
    if (( ${#farray} )); then
        if type zunit >/dev/null 2>&1; then
            [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                print -P -- "%F{38}test z-plugin: %F{154}RUNNING ZUNIT TESTS%f"
            integer quiet=0
            zstyle -T ":zi:annex:test" quiet && quiet=1
            if (( quiet )); then
                if zunit &>/dev/null; then
                    [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                        print -P -- "%F{38}test z-plugin: %F{154}The tests succeeded%f"
                else
                    [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                        print -P -- "%F{38}test z-plugin: %F{160}The tests have failed%f"
                fi
            else
                zunit
            fi
        else
            [[ "${OPTS[opt_-q,--quiet]}" != 1 ]] && \
                print -P -- "%F{38}test z-plugin: %F{154}Please install molovo/zunit to" \
                " run the zunit tests provided with the plugin%f"
        fi
    fi
)
